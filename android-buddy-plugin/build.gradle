apply plugin: 'java-gradle-plugin'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'kotlin-kapt'
apply plugin: "com.github.gmazzo.buildconfig"
apply from: "../common.gradle"

ext {
    tools_dependency_name = 'android-buddy-tools'
}

dependencies {
    implementation project(":$tools_dependency_name")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin-api:$kotlin_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.auto.factory:auto-factory:$autoFactory_version"
    implementation "io.github.classgraph:classgraph:4.8.87"
    compileOnly "com.android.tools.build:gradle:$androidBuild_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.auto.factory:auto-factory:$autoFactory_version"
    kapt "com.google.auto.value:auto-value:$autoValue_version"
    annotationProcessor "com.google.auto.factory:auto-factory:$autoFactory_version"
    annotationProcessor "com.google.auto.value:auto-value:$autoValue_version"
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation "com.android.tools.build:gradle:$androidBuild_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

buildConfig {
    packageName("com.likethesalad.${project.name.replaceAll("-", ".")}.generated")

    buildConfigField("String", "BYTE_BUDDY_VERSION", "\"$byteBuddy_version\"")
    buildConfigField(
            "String",
            "ANDROID_BUDDY_TOOLS_URI",
            "\"${project.group}:$tools_dependency_name:${project.version}\""
    )
}

gradlePlugin {
    plugins {
        byteBuddyApp {
            id = 'android-buddy'
            implementationClass = 'com.likethesalad.android.buddy.AndroidBuddyPlugin'
        }
        byteBuddyLibrary {
            id = 'android-buddy-library'
            implementationClass = 'com.likethesalad.android.buddylib.AndroidBuddyLibraryPlugin'
        }
    }
}

kapt {
    correctErrorTypes = true
}